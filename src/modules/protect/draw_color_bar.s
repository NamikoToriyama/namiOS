draw_color_bar:
        ;-----------------------------------------
        ; 【スタックフレームの構築】
        ;-----------------------------------------
        push ebp
        mov ebp, esp

        ;-----------------------------------------
        ; 【レジスタの保存】
        ;-----------------------------------------
        push eax
        push ebx
        push ecx
        push edx
        push esi
        push edi

        ;-----------------------------------------
        ; 基準となる位置をレジスタに保存
        ;-----------------------------------------
        mov esi, [ebp + 8]                          ; 列
        mov edi, [ebp + 12]                         ; 行

        ;-----------------------------------------
        ; カラーバーを表示
        ;-----------------------------------------
        mov ecx, 0
.10L:
        cmp ecx, 16
        jae .10E

        mov eax, ecx                                ; カラーバー表示処理
        and eax, 0x01
        shl eax, 3
        add eax, esi

        mov ebx, ecx                                ; 行位置を2回に一回の割合で加算する
        shr ebx, 1                                  ; ebx /= 2
        add ebx, edi                                ; ebx += y

        mov edx, ecx
        shl edx, 1
        mov edx, [.t0 + edx]

        cdecl draw_str, eax, ebx, edx, .s0          ; draw_str()

        inc ecx
        jmp .10L
.10E:

        ;-----------------------------------------
        ; 【レジスタの復帰】
        ;-----------------------------------------
        pop edi
        pop esi
        pop edx
        pop ecx
        pop ebx
        pop eax

        ;-----------------------------------------
        ; 【スタックフレームの破棄】
        ;-----------------------------------------
        mov esp, ebp
        pop ebp

        ret

.s0:    db '        ', 0                            ; 8文字分のスペース
.t0:    dw 0x0000                           ;背景色
        dw 0x0100
        dw 0x0200, 0x0A00
        dw 0x0300, 0x0B00
        dw 0x0400, 0x0C00
        dw 0x0500, 0x0D00
        dw 0x0600, 0x0E00
        dw 0x0700, 0x0F00
