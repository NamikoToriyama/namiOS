vga_set_read_plane:
        ;-----------------------------------------
        ; 【スタックフレームの構築】
        ;-----------------------------------------
        push ebp
        mov ebp, esp

        ;-----------------------------------------
        ; 【レジスタの保存】
        ;-----------------------------------------
        push eax
        push edx

        ;-----------------------------------------
        ; 読み込みプレーンの選択
        ;-----------------------------------------
        mov ah, [ebp + 8]                           ; AH = プレーンを選択
        and ah, 0x03                                ; AH &= 0x03
        mov al, 0x04                                ; AL = 読み込みマップ選択レジスタ
        mov dx, 0x03CE                              ; DX = グラフィックス制御ポート
        out dx, ax                                  ; ポート出力

        ;-----------------------------------------
        ; 【レジスタの復帰】
        ;-----------------------------------------
        pop edx
        pop eax

        ;-----------------------------------------
        ; 【スタックフレームの破棄】
        ;-----------------------------------------
        mov esp, ebp
        pop ebp

        ret


vga_set_write_plane:
        ;-----------------------------------------
        ; 【スタックフレームの構築】
        ;-----------------------------------------
        push ebp
        mov ebp, esp

        ;-----------------------------------------
        ; 【レジスタの保存】
        ;-----------------------------------------
        push eax
        push edx

        ;-----------------------------------------
        ; 書き込みプレーンの選択
        ;-----------------------------------------
        mov ah, [ebp + 8]                           ; 書き込みプレーンを指定
        and ah, 0x0F                                ; AH = 0x0F // 余計なビットをマスクする
        mov al, 0x02                                ; AL = マップマスクレジスタ
        mov dx, 0x03C4                              ; DX = シーケンサ制御ポート
        out dx, ax

        ;-----------------------------------------
        ; 【レジスタの復帰】
        ;-----------------------------------------
        pop edx
        pop eax

        ;-----------------------------------------
        ; 【スタックフレームの破棄】
        ;-----------------------------------------
        mov esp, ebp
        pop ebp

        ret

vram_font_copy:
        ;-----------------------------------------
        ; 【スタックフレームの構築】
        ;-----------------------------------------
        push ebp
        mov ebp, esp

        ;-----------------------------------------
        ; 【レジスタの保存】
        ;-----------------------------------------
        push eax
        push ebx
        push ecx
        push edx
        push esi
        push edi

        ;-----------------------------------------
        ; マスクデータの作成
        ;-----------------------------------------
        mov esi, [ebp + 8]                      ; ESI = フォントアドレス
        mov edi, [ebp + 12]                     ; edi = VRAMアドレス
        movzx eax, byte [ebp + 16]              ; プレーン
        movzx ebx, word [ebp + 20]              ; 色

        test bh, al                             ; ZF = (背景色 & プレーン)
        setz dh
        dec dh

        test bl, al
        setz dl
        dec dl

        ;-----------------------------------------
        ; 16ドットフォントのコピー
        ;-----------------------------------------
        cld                                       ; DF = 0; アドレス加算

        mov ecx, 16                               ; ecx = 16; 
.10L:

        ;-----------------------------------------
        ; フォントマスクの作成
        ;-----------------------------------------
        lodsb
        mov ah, al                                 ; AL = *ESI++ // フォント
        not ah

        ;-----------------------------------------
        ; 前景色
        ;-----------------------------------------
        and al, dl                                 ; AL = 前景色 & フォント

        ;-----------------------------------------
        ; 背景色
        ;-----------------------------------------
        test ebx, 0x0010
        jz .11F
        and ah, [edi]
        jmp .11E
.11F:
        and ah, dh
.11E:

        ;-----------------------------------------
        ; 前景色と背景色をコピー
        ;-----------------------------------------
        or al, ah                                  ; ここで背景色と前景色のor演算を取る

        ;-----------------------------------------
        ; 新しい値を出力
        ;-----------------------------------------
        mov [edi], al                              ; [EDI] = AL; プレーンに書き込む 

        add edi, 80
        loop .10L
.10E:

        ;-----------------------------------------
        ; 【レジスタの復帰】
        ;-----------------------------------------
        pop edi
        pop esi
        pop edx
        pop ecx
        pop ebx
        pop eax

        ;-----------------------------------------
        ; 【スタックフレームの破棄】
        ;-----------------------------------------
        mov esp, ebp
        pop ebp

        ret


vram_bit_copy:
        ;-----------------------------------------
        ; 【スタックフレームの構築】
        ;-----------------------------------------
        push ebp
        mov ebp, esp

        ;-----------------------------------------
        ; 【レジスタの保存】
        ;-----------------------------------------
        push eax
        push ebx
        push edi

        ;-----------------------------------------
        ; ビットのコピー
        ;-----------------------------------------
        mov edi, [ebp + 12]
        movzx eax, byte [ebp + 16]
        movzx ebx, word [ebp + 20]

        test bl, al
        setz bl
        dec bl

        ;-----------------------------------------
        ; 反転処理
        ;-----------------------------------------
        mov al, [ebp + 8]
        mov ah, al
        not ah

        ;-----------------------------------------
        ; 描画処理
        ;-----------------------------------------
        and ah, [edi]
        and al, bl
        or al, ah
        mov [edi], al

        ;-----------------------------------------
        ; 【レジスタの復帰】
        ;-----------------------------------------
        pop edi
        pop ebx
        pop eax

        ;-----------------------------------------
        ; 【スタックフレームの破棄】
        ;-----------------------------------------
        mov esp, ebp
        pop ebp

        ret
